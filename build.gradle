buildscript {
    configurations.all {
        resolutionStrategy.force 'net.java.dev.javacc:javacc:6.1.2'
    }
}

plugins {
    id "ca.coglinc.javacc" version "2.4.0"
}

plugins {
    id "org.sonarqube" version "2.6.1"
}
apply plugin: 'java'
apply plugin: 'findbugs'
apply plugin: 'checkstyle'

group 'fr.uga'
version '3.6.0'
sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'com.googlecode.json-simple', name: 'json-simple', version: '1.1.1'
    compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.11.0'
    compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.11.0'
    testCompile group: 'junit', name: 'junit', version: '4.11'
}

compileJavacc {
    arguments = [debug_parser: 'false']
    inputDirectory = file('src/main/java/fr/uga/pddl4j/parser/lexer/')
    outputDirectory = file('src/main/java/fr/uga/pddl4j/parser/lexer/')
}

checkstyle {
    toolVersion = "8.9"
}

jar {
    manifest {
        attributes 'Implementation-Title': 'Gradle Quickstart',
                'Implementation-Version': version,
                'Gradle-Version':'Gradle 4.7',
                'Built-By': 'Damien Pellier',
                'Premain-Class': 'fr.uga.pddl4j.util.MemoryAgent',
                'Main-Class': 'fr.uga.pddl4j.planners.hsp.HSP'
    }
    doFirst {
        from { configurations.runtime.collect { it.isDirectory() ? it : zipTree(it) } }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.7'
}

task run (type: JavaExec, dependsOn: build){
    if (project.hasProperty("Args")) {
        println("arguments list : " + Args)
        args(Args.split(','))
    }

    jvmArgs = ["-javaagent:"+project.buildDir.name+"/libs/"+project.name+"-"+project.version+".jar", "-server", "-Xms2048m", "-Xmx2048m"]

    description = "Run HSP plannin algorithm"
    main = "fr.uga.pddl4j.planners.hsp.HSP"
    classpath = sourceSets.main.runtimeClasspath
}

task cleanGenerated(type:Delete) {
    delete fileTree(dir: "src/main/java/fr/uga/pddl4j/parser/lexer", exclude:"Lexer.jj")
}

clean.doLast() {
    cleanGenerated.execute()
}

findbugsMain.onlyIf() { project.hasProperty('findbug') }
findbugsTest.onlyIf() { project.hasProperty('findbug') }

findbugsMain.doLast() {
    println("Find bug process completed without finding an issue")
}

findbugs {
    effort= "max"
    reportLevel = "low"
    excludeFilter = file("$rootProject.projectDir/config/findbugs/excludeFilter.xml")
}

javadoc.doFirst() {
    excludeLexer(javadoc)
    failOnError false
}

checkstyleMain.onlyIf() { !project.hasProperty('noCheckStyle') }

checkstyleMain.doFirst() {
    excludeLexer(checkstyleMain)
}

checkstyleMain.doLast() {
    printCheckstyleVersion()
}

checkstyleTest.onlyIf() { !project.hasProperty('noCheckStyle') }

checkstyleTest.doFirst(){
    excludeLexer(checkstyleTest)
}

checkstyleTest.doLast(){
    printCheckstyleVersion()
}

test {
    // set heap size for the test JVM(s)
    minHeapSize = "512m"
    maxHeapSize = "2048m"
    forkEvery = 1 // One new JVM for each test
}

defaultTasks 'clean', 'build', 'test'

// Exclude lexer source form task
def excludeLexer(task) {
    task.exclude("**/lexer/**")
}

// Display the current version use by checkstyle plugin
def printCheckstyleVersion(){
    println("Checkstyle version: "+checkstyle.toolVersion)
}
